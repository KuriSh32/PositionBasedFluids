#version 430 core

layout(local_size_x = 256) in;

layout(std430, binding = 1) buffer PositionPredict {
    vec4 positionPredict[];
};

layout(std430, binding = 12) buffer Lambda {
    float lambda[];
};

layout(std430, binding = 13) buffer DeltaPosition {
    vec4 deltaPosition[];
};

layout(std430, binding = 7) buffer NeighborCountPerParticle {
    uint neighborCountPerParticle[];
};

layout(std430, binding = 8) buffer NeighborIndexBuffer {
    uint neighborIndexBuffer[];
};

uniform float KERNEL_RADIUS;
uniform float MASS;
uniform float REST_DENSITY_REVERSE;
uniform float PI;
uniform uint MAX_NEIGHBOR_COUNT;
uniform uint PARTICLE_COUNT;

vec3 SpikyGradient(vec3 r, float h) {
    float r_mag = length(r);
    if (r_mag > h) {
        return vec3(0.0);
    }
    else {
        if (r_mag < 0.00001) {
            return vec3(0.0);
        }
        else {
            float fac1 = -45.0 / (PI * pow(h, 6));
            float fac2 = pow(h - r_mag, 2);
            return normalize(r) * fac1 * fac2;
        }
    }
}

void main() {
    uint index = gl_GlobalInvocationID.x;
    if (index >= PARTICLE_COUNT) {
        return;
    }
    vec3 dPosition = vec3(0.0);
    for (uint i = 0; i < neighborCountPerParticle[index]; i++) {
        uint neighborIndex = neighborIndexBuffer[index * MAX_NEIGHBOR_COUNT + i];
        dPosition += (lambda[index] + lambda[neighborIndex]) * SpikyGradient(vec3(positionPredict[index]) - vec3(positionPredict[neighborIndex]), KERNEL_RADIUS);
    }
    dPosition *= MASS * REST_DENSITY_REVERSE;
    deltaPosition[index] = vec4(dPosition, 0.0);
}
